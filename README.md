

# `codec_JSON` examples

The JSON encoder offers the option to substitute atoms contained in the
input data with arbitrary values. The atoms can optionally be encoded as
string values if the encoder is called with the option switch turned on:

    1> Out = codec_JSON:encode(
      {1,c,{3,4,#{"a" => a, "b" => b}}},
      #{atomic_names => true}).
    <<"[1,\"c\",[3,4,{\"b\":\"b\",\"a\":\"a\"}]]">>
    2> io:put_chars([Out,$\n]).
    [1,"c",[3,4,{"b":"b","a":"a"}]]
    ok

The default is to handle atoms by requesting substitutions as these are
encountered while processing the input data.

Kick off the encoding process by offering data that contains `atom()`
symbols to represent place-holders:

    1> {c,Fn0} = codec_JSON:encode({1,c,{3,4,#{"a" => a, "b" => b}}}).
    {c,#Fun<codec_JSON.1.26682825>}

The encoder returns a two-tuple, the first element is the atom `'c'`, this
serves as a key to identify what value to insert at that position.  Here we
choose to insert an array `{11,22,33}` (Erlang tuples are encoded as JSON
arrays).

The returned function in `1>` is called with the `{11,22,33}` substitution
for `'c'` in the encoded output.  The next place-holder returned by the
encoder is the atom `'b'`:

    2> {b,Fn1} = Fn0({11,22,33}).
    {b,#Fun<codec_JSON.1.26682825>}

For `'b'` the object `#{"cc" => 44, "dd" => 55}` is substituted (Erlang maps
are encoded as JSON objects):

    3> {a,Fn2} = Fn1(#{"cc" => 44, "dd" => 55}).
    {a,#Fun<codec_JSON.1.26682825>}

The last place-holder is atom `'a'`, which is substituted with a string
value `"END"`:

    4> Bin = Fn2("END").
    <<"[1,[11,22,33],[3,4,{\"b\":{\"dd\":55,\"cc\":44},\"a\":\"END\"}]]">>

Display the encoded binary in more friendly fashion:

    5> io:put_chars([Bin,$\n]).
    [1,[11,22,33],[3,4,{"b":{"dd":55,"cc":44},"a":"END"}]]
    ok

The above substitution values can be provided to the enoder as a map:

    6> codec_JSON:encode(
         {1,c,{3,4,#{"a" => a, "b" => b}}},
         #{ substitute_map =>
            #{ a => "END",
               b => #{"cc" => 44, "dd" => 55},
               c => {11,22,33} } } ).
    <<"[1,[11,22,33],[3,4,{\"b\":{\"dd\":55,\"cc\":44},\"a\":\"END\"}]]">>

The substitutions are recursive: If the substitute also contains atoms,
these will either be fetched from the `substitute_map`, or control is passed
back to the caller to decide.

    7> codec_JSON:encode(
         {1,c,{3,4,#{"a" => a, "b" => b}}},
         #{ substitute_map =>
            #{ a => "END",
               b => #{"cc" => cc, "dd" => dd}, % contains atoms
               c => {11,22,33},
               cc => 4444,
               dd => 5555 } } ).
    <<"[1,[11,22,33],[3,4,{\"b\":{\"dd\":5555,\"cc\":4444},\"a\":\"END\"}]]">>

Recursion depth is limited by default to 1000. The limit value can be
specified as an option:

    8> codec_JSON:encode(
         {1,c,{3,4,#{"a" => a, "b" => b}}},
         #{ looplimit => 123,
            substitute_map =>
            #{ a => a,
               b => b,
               c => c } } ).
    ** exception throw: looplimit
         in function  codec_JSON:encode_substitutions/2 (codec_JSON.erl, line 88)

# Extract data from XSD

The `decode_XML` module demonstrates how to parse XML data, and to
manipulate this data. The example below uses data from the ISO20022 message
definition contained in an XSD file.

Load the entire binary data from the XSD file:

    1> {ok,Bin} = file:read_file("ISO20022/pacs.002.001.13.xsd").
    {ok,<<"<?xml version=\"1.0\" encoding=\"UTF-8\"?>\r\n"
          "<!--Generated by Standards Editor (build:R1.6.22)"
          " on 2023 Feb 09 10:"...>>}

First pass decode of the XML data in the binary:

    2> Pacs002 = decode_XML:document(Bin).
    [{prolog,<<"xml version=\"1.0\" encoding=\"UTF-8\"">>},
     {comment,
         <<"Generated by Standards Editor (build:R1.6.22)"
           " on 2023 Feb 09 10:22:51, ISO 20022 version : 2013">>},
     {element,<<"xs:schema">>,
         #{<<"elementFormDefault">> => <<"qualified">>,
           <<"targetNamespace">> =>
               <<"urn:iso:std:iso:20022:tech:xsd:pacs.002.001.13">>,
           <<"xmlns">> =>
               <<"urn:iso:std:iso:20022:tech:xsd:pacs.002.001.13">>,
           <<"xmlns:xs">> => <<"http://www.w3.org/2001/XMLSchema">>},
         [<<"\r\n    ">>,
          {element,<<"xs:element">>,
              #{<<"name">> => <<"Document">>,<<"type">> => <<"Document">>},
              empty},
          <<"\r\n    ">>,
          {element,<<"xs:complexType">>,
              #{<<"name">> => <<"AccountIdentification4Choice">>},
              [<<"\r\n        ">>,
               {element,<<"xs:choice">>,#{},
                   [<<"\r\n            ">>,
                    {element,<<"xs:element">>,
                        #{<<"name">> => <<"IBAN">>,
                          <<"type">> => <<"IBAN2007"...>>},
                        empty},
                    <<"\r\n            ">>,
                    {element,<<"xs:eleme"...>>,
                        #{<<...>> => <<...>>,...},
                        empty},
                    <<"\r\n        ">>]},
               <<"\r\n    ">>]},
          <<"\r\n    ">>,{...}|...]}]

Inspect the raw text embedded in the markup. This is all whitespace, and the
result is an empty binary after the leading spaces are trimmed:

    3> decode_XML:raw_text(Pacs002).
    <<>>

The XML namespace prefixes are garbage, get rid of that design-by-committee
junk:

    4> Trim = decode_XML:trim_namespace(Pacs002).

Now we can see the trees in the woods. the result is a searchable Schema
represented as a map:

    5> Schema = decode_XML:schema(Trim).
    #{<<"Frequency36Choice">> =>
          #{name => <<"Frequency36Choice">>,
            choice =>
                [#{name => <<"Tp">>,type => <<"Frequency6Code">>},
                 #{name => <<"Prd">>,type => <<"FrequencyPeriod1">>},
                 #{name => <<"PtInTm">>,
                   type => <<"FrequencyAndMoment1">>}]},
      <<"LEIIdentifier">> =>
          #{name => <<"LEIIdentifier">>,type => string,
            pattern => <<"[A-Z0-9]{18,18}[0-9]{2,2}">>},
      <<"AddressType3Choice">> =>
          #{name => <<"AddressType3Choice">>,
            choice =>
                [#{name => <<"Cd">>,type => <<"AddressType2Code">>},
                 #{name => <<"Prtry">>,
                   type => <<"GenericIdentification30">>}]},...}

The XSD top-level is a `Document` element:

    6> maps:get(<<"Document">>,Schema).
    #{name => <<"Document">>,
      sequence =>
          [#{name => <<"FIToFIPmtStsRpt">>,
             type => <<"FIToFIPaymentStatusReportV13">>}]}

It is now possible to search for the `type` that encodes the `Document`
contents, in a type called `FIToFIPaymentStatusReportV13`:

    7> maps:get(<<"FIToFIPaymentStatusReportV13">>,Schema).
    #{name => <<"FIToFIPaymentStatusReportV13">>,
      sequence =>
          [#{name => <<"GrpHdr">>,type => <<"GroupHeader101">>},
           #{name => <<"OrgnlGrpInfAndSts">>,
             type => <<"OriginalGroupHeader17">>,minOccurs => 0,
             maxOccurs => infinity},
           #{name => <<"TxInfAndSts">>,
             type => <<"PaymentTransaction142">>,minOccurs => 0,
             maxOccurs => infinity},
           #{name => <<"SplmtryData">>,type => <<"SupplementaryData1">>,
             minOccurs => 0,maxOccurs => infinity}]}

Within the `FIToFIPaymentStatusReportV13` there is a sequence of elements:

    8> maps:get(<<"GroupHeader101">>,Schema).
    #{name => <<"GroupHeader101">>,
      sequence =>
          [#{name => <<"MsgId">>,type => <<"Max35Text">>},
           #{name => <<"CreDtTm">>,type => <<"ISODateTime">>},
           #{name => <<"InstgAgt">>,
             type => <<"BranchAndFinancialInstitutionIdentification6">>,
             minOccurs => 0,maxOccurs => 1},
           #{name => <<"InstdAgt">>,
             type => <<"BranchAndFinancialInstitutionIdentification6">>,
             minOccurs => 0,maxOccurs => 1},
           #{name => <<"OrgnlBizQry">>,
             type => <<"OriginalBusinessQuery1">>,minOccurs => 0,
             maxOccurs => 1}]}

One of those elements is called `ISODateTime`. Lets have a closer look at
that type definition:

    9> maps:get(<<"ISODateTime">>,Schema).
    #{name => <<"ISODateTime">>,type => dateTime}

This is a fundamental type that e.g. a message generator can produce
random (or restrained) values for.

The range of fundamental types identified so far in the example above are:

* `date`
* `dateTime`
* `gYear`
* `boolean`
* `base64Binary`
* `decimal`
* `string`

These fundamental types can be combined to compose either a simpleType or a
complexType.

The composition of a complexType can either be:

* `simpleContent`
* `sequence`
* `choice`

